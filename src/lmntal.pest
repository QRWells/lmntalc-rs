COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Program = { SOI ~ WorldProcessList ~ EOI }

WorldProcessList = { ((Rule | DeclarationList) ~ ".")+ }

DeclarationList = { Declaration ~ ("," ~ Declaration)* }

Declaration = { UnitAtom }

Rule = { (RuleName ~ "@@")? ~ DeclarationList ~ ":-" ~ DeclarationList }

Atom = { AtomName ~ ("(" ~ DeclarationList ~ ")")? }

Link = { LinkName }

UnitAtom = { Atom | Membrane | Link }

Membrane = { AtomName? ~ "{" ~ WorldProcessList ~ "}" }

AtomName = @{
    (ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")*)
  | ASCII_DIGIT+
}

LinkName = @{
    (ASCII_ALPHA_UPPER | "_") ~ (ASCII_ALPHANUMERIC | "_")*
}

RuleName = @{ AtomName | LinkName }
