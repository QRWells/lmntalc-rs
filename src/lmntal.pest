COMMENT    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

Program = { SOI ~ WorldProcessList ~ EOI }

WorldProcessList = { ((Rule | DeclarationList) ~ ";")+ }

DeclarationList = { Declaration ~ ("," ~ Declaration)* }

Declaration = { UnitAtom }

Rule = { (RuleName ~ ":")? ~ DeclarationList ~ ("[" ~ Guard ~ "]")? ~ "->" ~ DeclarationList? }

LogicalOperator    = { "&&" | "||" }
RelationalOperator = { "==" | "!=" | "<=" | "<" | ">=" | ">" }
ArithmeticOperator = { "+" | "-" | "*" | "/" | "%" }
RelationalOperand  = { UnitAtom ~ (ArithmeticOperator ~ UnitAtom)? }
LogicalOperand     = { RelationalOperand ~ (RelationalOperator ~ RelationalOperand)? | "(" ~ Guard ~ ")" }
Guard              = { LogicalOperand ~ (LogicalOperator ~ LogicalOperand)* }

Atom = { AtomName ~ ("(" ~ DeclarationList ~ ")")? }

Link = { LinkName }

UnitAtom = { Membrane | Atom | Link }

Membrane = { AtomName? ~ "{" ~ WorldProcessList ~ "}" }

AtomName = @{
    (ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")*)
  | ASCII_DIGIT+
}

LinkName = @{
    (ASCII_ALPHA_UPPER | "_") ~ (ASCII_ALPHANUMERIC | "_")*
}

RuleName = @{ AtomName | LinkName }

String = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
Integer = @{ ASCII_DIGIT+ }